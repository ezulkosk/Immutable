/*
 * Type system for object initialization, as described in:
 * "Freedom Before Commitment" by Alexander J. Summers and 
 * Peter Muller. 
 * 
 * Implemented in conjunction with an immutable type system.
 * 
 * Course Project: ECE750 Spring 2013
 * Author: Ed Zulkoski
 */
package javacop.immutability;
flowfact ImmutabilityFlowFacts;

/*---------------------------------------*/
/* Define the initialization annotations */
/*---------------------------------------*/

declare free(Symbol s){
	require(s.hasAnnotation("javacop.annotations.Free"));
}

declare unclassified(Symbol s){
	require(s.hasAnnotation("javacop.annotations.Unclassified"));
}

// Default to Committed
declare committed(Symbol s){
	require(explicitlyCommitted(s) || (!free(s) && !unclassified(s)));
}

declare committed(JCTree t){
	require(t.holdsSymbol && committed(t.getSymbol));
}

declare explicitlyCommitted(JCTree t){
	require(t.holdsSymbol && explicitlyCommitted(t.getSymbol));
}

declare explicitlyCommitted(Symbol s){
	require(s.hasAnnotation("javacop.annotations.Committed"));
}

/*-----------------------------------*/
/* Define the mutability annotations */
/*-----------------------------------*/

declare mutable(Symbol s){
	require(s.hasAnnotation("javacop.annotations.Mutable"));
}

// Default to immutable
declare immutable(Symbol s){
	require(s.hasAnnotation("javacop.annotations.Immutable")
			|| !mutable(s));
}

declare immutable(JCTree t){
	require(t.holdsSymbol && immutable(t.getSymbol));
}

/*----------------*/
/* Subtype Checks */
/*----------------*/

//Ensures that:
// 1) A mutable cannot be assigned to an immutable, or vice-versa.
// 2) The initialization types conform (e.g. a free object cannot be stored in a committed object).
rule subtypeCheck(r <<: l){
	require(mutability_helper(l, r)):warning(r,
			"Cannot assign mutable to immutable or vice versa: " + l + " <- " + r);
	require(ImmutabilityFlowFacts ff; ff <- r#getFlowFacts(ImmutabilityFlowFacts#class)){ //this line is only used for error messages
		require(init_helper(l,r)):warning(r,
				"Incompatible init types: " + l + " <- " + r);
	}
}


/*------------------*/
/* Mutability Rules */
/*------------------*/

declare mutability_helper(Symbol l, JCTree r){
	require(ImmutabilityFlowFacts ff; ff <- r#getFlowFacts(ImmutabilityFlowFacts#class)){
		require(ff#mutabilityCheck(ff#immType(l, env), ff#immType(r, env)));
	}
}

// I'm unsure if this is ever called. Might be able to remove.
declare mutability_helper(JCTree l, JCTree r){
	require(ImmutabilityFlowFacts ff; ff <- r#getFlowFacts(ImmutabilityFlowFacts#class)){
		require(ff#mutabilityCheck(ff#immType(l, env), ff#immType(r, env)));
	}
}

rule no_mutables_in_immutable_constructor(r <<: l){
	where(r instanceof JCNewClass){
		require(ImmutabilityFlowFacts ff; ff <- r#getFlowFacts(ImmutabilityFlowFacts#class)){
			require(ff#constructorCheck(ff#immType(l, env), ff#checkConstructorArgs(r, env))):warning(r, 
					"Passing a mutable into an immutable object constructor.");
		}
	}	
}

/*----------------------*/
/* Initialization Rules */
/*----------------------*/

declare init_helper(Symbol l, JCTree r){
	require(ImmutabilityFlowFacts ff; ff <- r#getFlowFacts(ImmutabilityFlowFacts#class)){
		require(ff#initCheck(ff#initType(l, env), ff#initType(r, env)));
	}
}

// I'm unsure if this is ever called. Might be able to remove.
declare init_helper(JCTree l, JCTree r){
	require(ImmutabilityFlowFacts ff; ff <- r#getFlowFacts(ImmutabilityFlowFacts#class)){
		require(ff#initCheck(ff#initType(l, env), ff#initType(r, env)));
	}
}

declare definitelyAssignedVariable(JCAssign a){
	require(ImmutabilityFlowFacts ff ; ff <- a#getFlowFacts(ImmutabilityFlowFacts#class)){
		require(ff#hasBeenInitialized(a.lhs));
	}
}


rule assign(JCAssign a){
	where(a.lhs.holdsSymbol && committed(a.lhs.getSymbol)){
		require(committed(a.rhs) /*definitelyAssignedVariable(a)*/): warning(a, "Left committed but right uncommitted.");
	}
}

// T_Var_Ass
// if lhs is a field access of a committed immutable object, error 
//  (unless it's a self-assignment (a.lhs = a.lhs), but not worth handling)
rule tvarass(JCAssign a){
	require(1 < 2);
}



/*--------------------*/
/* Sanity Constraints */	
/*--------------------*/

//A node must satisfy "free xor committed xor unclassified"
declare singleInitAnnotation(Symbol s){
	require(!(free(s) && explicitlyCommitted(s)) 
			&& !(free(s) && unclassified(s))
			&& !(explicitlyCommitted(s) && unclassified(s)));
}

rule singleInitAnnotation(JCVariableDecl vd){
	where(vd.holdsSymbol){
		require(singleInitAnnotation(vd.getSymbol))
		:warning(vd,"VarDecl can have only one of: [@Free,@Classified,@Unclassified].");
	}
}

rule singleInitAnnotation(JCMethodDecl md){
	where(md.holdsSymbol){
		require(singleInitAnnotation(md.getSymbol))
		:warning(md,"MethodDecl can have only one of: [@Free,@Classified,@Unclassified].");
	}
}

//A node must satisfy "mutable xor immutable"
declare singleMutationAnnotation(Symbol s){
	require(mutable(s) ^ immutable(s));
}

rule singleMutationAnnotation(JCVariableDecl vd){
	where(vd.holdsSymbol){
		require(singleMutationAnnotation(vd.getSymbol)):warning(vd,"VarDecl can have only one of: [@Mutable,@Immutable].");
	}
}

rule singleMutationAnnotation(JCMethodDecl md){
	where(md.holdsSymbol){
		require(singleMutationAnnotation(md.getSymbol)):warning(md,"MethodDecl can have only one of: [@Mutable,@Immutable].");
	}
}

//A constructor should not have an initialization modifier
declare noInitAnnotation(Symbol s){
	require(!free(s) && !explicitlyCommitted(s) && !unclassified(s));
}

rule noInitAnnotation(JCMethodDecl md){
	where(md.holdsSymbol && md.getSymbol.isConstructor){
		require(noInitAnnotation(md.getSymbol)):warning(md, "Constructors should not have initialization annotations.");
	}
}

//A field should not have an initialization modifier
rule noInitAnnotation(JCVariableDecl vd){
	where(vd.holdsSymbol && !vd.getSymbol.isStatic && !vd.getSymbol.isLocal){
		require(noInitAnnotation(vd.getSymbol)):warning(vd, "Fields should not have initialization annotations.");
	}
}

/*-----------------*/
/* TODO Extensions */
/*-----------------*/

//currently don't allow static fields
rule noStaticFields(JCVariableDecl vd){
	require(!vd.sym.isStatic):warning(vd, "Static fields are currently not allowed.");
}

